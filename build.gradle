apply plugin: 'com.android.library'
apply plugin: 'findbugs'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 21
        versionCode 5
        versionName "1.0.6"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            libraryVariants.all { variant ->
                variant.outputs.each { output ->
                    def date = new Date()
                    def formattedDate = date.format('yyMMddHH')
                    //noinspection GroovyAssignabilityCheck
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace("-release", "_v" + defaultConfig.versionName + "_build" + defaultConfig.versionCode + "_" + formattedDate + "_release"))
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "low"
    excludeFilter = new File("${project.rootDir}/config/findbugs/findbugs_filter.xml")
    classes = files("build/intermediates/classes/")
    source 'scr/main'
    include '**/*.java'
    reports {
        xml {
            destination "${project.rootDir}/build/reports/findbugs_report.xml"
        }
    }
    classpath = files()
}

check.dependsOn 'findbugs'